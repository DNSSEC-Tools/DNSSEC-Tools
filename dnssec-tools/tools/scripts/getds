#!/usr/bin/perl
#
# Copyright 2008-2009 SPARTA, Inc.  All rights reserved.  See the COPYING
# file distributed with this software for details.
#
#
# getds
#

use Net::DNS;
use Net::DNS::SEC;
use Data::Dumper;
use Net::DNS::SEC::Tools::QWPrimitives;

my %opts =
  ( 'a' => 'SHA256,SHA1'
  );

DTGetOptions(\%opts,
		['GUI:VERSION',"0.1\nDNSSEC-Tools Version: 1.4"],

		['a|hash-algorithm=s',
		 'Hash algorithm to use (SHA256 or SHA1)'],
		['z|print-zsks',     'Print ZSK DS records as well as KSKs'],

	        ['p|dont-check-parent', 'Don\'t check for the parent\'s published DS record'],
		['GUI:otherargs_text',"DOMAIN_NAME"],
	       ) || exit;

# query the DNSKEY from the net
my $res  = Net::DNS::Resolver->new;
my $q = $res->query($ARGV[0], "DNSKEY");
my @keys = $q->answer;

# Generate a list of all the algorithms to use:
my @algorithms = split(/,\s*/, uc($opts{'a'}));

#
# for each of the keys found, print out the DS record
#
my @childds;
my %childds;
print "--- DS records generated from queried keys of $ARGV[0]:\n";
foreach my $key (@keys) {
    next if (($key->flags & 0x1) == 0);  # KSKs only
    $key->{'name'} = $ARGV[0];

    foreach my $algorithm (@algorithms) {
	my $ds = create Net::DNS::RR::DS($key,
					 digtype => $algorithm,
					);
	push @childds, $ds;
	print make_printing_ds($ds);
	$childds{make_matching_ds($ds)} = make_printing_ds($ds);
    }
}

exit if ($opts{'p'});

# query the DNSKEY from the net
$q = $res->query($ARGV[0], "DS");

my @dses;
if ($q) {
    @dses = $q->answer;
}

my @errors;
print "\n--- DS records pulled from the parent of $ARGV[0]:\n";
foreach my $ds (@dses) {
    print make_printing_ds($ds);
    my $parentds = make_matching_ds($ds);
    if (exists($childds{$parentds})) {
	delete $childds{$parentds};
    } else {
	push @errors, "The following DS record exists in the parent, but no child key matches it:  \n" . make_printing_ds($ds) . "\n";
    }
}

foreach my $ds (keys(%childds)) {
    push @errors, "The following DS record for a child key is not published by the parent:\n" . "$childds{$ds}\n";
}

if ($#errors == -1) {
    print "\nErrors Found:  NONE (everything matches)\n";
    exit 0;
}

print "\nERRORS (" . ($#errors+1) . "):\n";
foreach my $error (@errors) {
    print $error,"\n";
}

sub make_matching_ds {
    my $changethis = $_[0];
    $changethis = $changethis->string;
    $changethis = lc($changethis);
    $changethis =~ s/.*\sds\s+//;
    $changethis =~ s/\;.*//;
    return $changethis;
}

sub make_printing_ds {
    my $changethis = $_[0];
    $changethis = $changethis->string;
    $changethis = uc($changethis);
    $changethis =~ s/\;.*//;
    $changethis = "  " . $changethis . "\n";
    return $changethis;
}

exit 1;

=head1 NAME

getds - Create a DS record from DNSKEYing information

=head1 SYNOPSIS

getds <domain>

=head1 DESCRIPTION

B<getds> will create a DS record from DNSKEYs for the specified DNS
domain.  It does this by converting DNSKEYs to DS records using the
specified hashing algorithm.  The results can then be passed to
upstream DNSSEC-supporting parents or to DLV registries.

B<getds> will also pull the parent's published DS records and compare
them against the existing keys.  It will then list any DS records not
published in the parent, as well as any DS records that are published
in the parent but which don't match an existing key.

=head1 OPTIONS

B<getds> takes the following options:

=over 4

=item B<-a ALGORITHMS>

=item B<--hash-algorithm algorithm ALGORITHMS>

This option specifies the hash algorithm to use when converting
DNSKEYs to DS records.  It may be a comma separated list if multiple
algorithms are desired.  The algorithms to choose from may be either
SHA256 or SHA1.

The default is SHA256,SHA1

=item B<-z>

=item B<--print-zsks>

This option causes B<getds> to print ZSK DS records, as well as KSK records.

=item B<-p>

=item B<--dont-check-parent>

Instructs B<getds> to not check the records in the parent for their
published DS records.

=back

=head1 SECURITY CONSIDERATIONS

By default, getds pulls data from the live DNS.  If your DNS resolver
isn't configured so that this is pulled securely, then the results
can't be trusted.

=head1 COPYRIGHT

Copyright 2008-2009 SPARTA, Inc.  All rights reserved.
See the COPYING file included with the DNSSEC-Tools package for details.

=head1 AUTHOR

Wes Hardaker, hardaker AT AT AT users.sourceforge.net

=cut

