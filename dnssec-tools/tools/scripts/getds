#!/usr/bin/perl
#
# Copyright 2008-2009 SPARTA, Inc.  All rights reserved.  See the COPYING
# file distributed with this software for details.
#
#
# getds
#

use Net::DNS;
use Net::DNS::SEC;
use Data::Dumper;
use Net::DNS::SEC::Tools::QWPrimitives;

my %opts =
  ( 'a' => 'SHA256'
  );

DTGetOptions(\%opts,
		['GUI:VERSION',"0.1\nDNSSEC-Tools Version: 1.4"],

		['a|hash-algorithm=s',
		 'Hash algorithm to use (SHA256 or SHA1)'],
		['z|print-zsks',     'Print ZSK DS records as well as KSKs'],

	        ['p|dont-check-parent', 'Don\'t check for the parent\'s published DS record'],
		['GUI:otherargs_text',"DOMAIN_NAME"],
	       ) || exit;

# query the DNSKEY from the net
my $res  = Net::DNS::Resolver->new;
my $q = $res->query($ARGV[0], "DNSKEY");
my @keys = $q->answer;

#
# for each of the keys found, print out the DS record
#
my @childds;
my %childds;
print "--- DS records generated from queried keys of $ARGV[0]:\n";
foreach my $key (@keys) {
    next if (($key->flags & 0x1) == 0);  # KSKs only
    $key->{'name'} = $ARGV[0];

    my $ds = create Net::DNS::RR::DS($key,
				     digtype => $opts{'a'},
				    );
    push @childds, $ds;
    print lc($ds->string);
    $childds{make_matching_ds($ds->string)} = lc($ds->string);
}

exit if ($opts{'p'});

# query the DNSKEY from the net
$q = $res->query($ARGV[0], "DS");

my @dses;
if ($q) {
    @dses = $q->answer;
}

my @errors;
print "\n--- DS records pulled from the parent of $ARGV[0]:\n";
foreach my $ds (@dses) {
    $ds->print;
    my $parentds = make_matching_ds($ds->string);
    print "here: $parentds\n";
    if (exists($childds{$parentds})) {
	delete $childds{$parentds};
    } else {
	push @errors, "The following DS record exists in the parent, but no child key matches it:  \n" . lc($ds->string) . "\n";
    }
}
if ($#$dses == -1) {
    print "  NONE\n";
}

foreach my $ds (keys(%childds)) {
    push @errors, "The following DS record for a child key is not published by the parent:\n" . "$ds\n";
}

if ($#errors == -1) {
    print "Errors Found:  NONE\n";
    exit 0;
}

print "\nERRORS (" . ($#errors+1) . "):\n";
foreach my $error (@errors) {
    print $error,"\n";
}

sub make_matching_ds {
    my $changethis = $_[0];
    $changethis = lc($changethis);
    $changethis =~ s/.*\sds\s+//;
    $changethis =~ s/\;.*//;
    return $changethis;
}

exit 1;

=head1 NAME

getds - Create a DS record from DNSKEYing information

=head1 SYNOPSIS

getds <domain>

=head1 DESCRIPTION

B<getds> will create a DS record from DNSKEYs for the specified DNS
domain.  It does this by converting DNSKEYs to DS records using the
specified hashing algorithm.  The results can then be passed to
upstream DNSSEC-supporting parents or to DLV registries.

B<getds> will also pull the parent's published DS records and compare
them against the existing keys.  It will then list any DS records not
published in the parent, as well as any DS records that are published
in the parent but which don't match an existing key.

=head1 OPTIONS

B<getds> takes the following options:

=over 4

=item B<-a>

=item B<--hash-algorithm algorithm>

This option specifies the hash algorithm to use when converting DNSKEYs to DS
records.  This may be either SHA256 (the default) or SHA1.
B<-a> is an alias for this option.

=item B<-z>

=item B<--print-zsks>

This option causes B<getds> to print ZSK DS records, as well as KSK records.
B<-z> is an alias for this option.

=item B<-p>

=item B<--dont-check-parent>

Instructs B<getds> to not check the records in the parent for their
published DS records.

=back

=head1 SECURITY CONSIDERATIONS

By default, getds pulls data from the live DNS.  If your DNS resolver
isn't configured so that this is pulled securely, then the results
can't be trusted.

=head1 COPYRIGHT

Copyright 2008-2009 SPARTA, Inc.  All rights reserved.
See the COPYING file included with the DNSSEC-Tools package for details.

=head1 AUTHOR

Wes Hardaker, hardaker AT AT AT users.sourceforge.net

=cut

