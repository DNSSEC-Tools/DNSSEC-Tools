#!/usr/bin/perl
#
# Copyright 2004 Sparta, inc.  All rights reserved.  See the COPYING
# file distributed with this software for details
#
#
# lskrf
#
#	This script lists fields in a keyrec file.
#

use strict;

use Getopt::Long;

use Net::DNS::SEC::Tools::keyrec;
use Net::DNS::SEC::Tools::tooloptions;

#######################################################################

#
#
my $DAY	  = (24 * 60 * 60);		# Seconds in a day.
my $WEEK  = (7 * $DAY);			# Seconds in a week.

my $DAYSWARN = 7;			# Days before an expiration warning.

#######################################################################

#
# Data required for command line options.
#
my %options = ();			# Filled option array.
my @opts =
(
	"all",				# List all keyrecs.
	"zones",			# List zone keyrecs.
	"keys",				# List key keyrecs.
	"ksk",				# List KSK keyrecs.
	"zsk",				# List ZSK keyrecs.
	"cur",				# List current key keyrecs.
	"new",				# List new key keyrecs.
	"pub",				# List published key keyrecs.

	"ref",				# List referenced key keyrecs.
	"unref",			# List unreferenced key keyrecs.
	"valid",			# List unexpired zone keyrecs.
	"expired",			# List expired zone keyrecs.

	"cnt",				# Only give a count of matching keyrecs.
	"terse",			# Give terse output.
	"long",				# Give long output.

	"help",				# Give a usage message and exit.
);

# options to be completed:
# 	expired
# 	valid

#
# Flag values for the various options.  Variable/option connection should
# be obvious.
#
my $allflag;
my $zonesflag;
my $keysflag;
my $kskflag;
my $zskflag;
my $zcurflag;
my $znewflag;
my $zpubflag;

my $refflag;
my $unrefflag;
my $validflag;
my $expiredflag;

my $cntflag;
my $terse;
my $long;

my $count   = 0;			# Record-match count.

#######################################################################


my @krnames;				# List of keyrecs in the file.

my %zones    = ();			# Names of zone keyrecs.
my %allkeys  = ();			# Names of all keyrecs.
my %kskkeys  = ();			# Names of KSK keyrecs.
my %zskkeys  = ();			# Names of ZSK keyrecs.
my %zcurkeys = ();			# Names of current ZSK keyrecs.
my %znewkeys = ();			# Names of new ZSK keyrecs.
my %zpubkeys = ();			# Names of published ZSK keyrecs.

my $ret;				# Return code from main().

$ret = main();
exit($ret);

#-----------------------------------------------------------------------------
#
# Routine:	main()
#
sub main()
{
	my $argc = @ARGV;		# Number of command line arguments.
	my $errors = 0;			# Total error count.

	#
	# Check our options.
	#
	doopts($argc);

	#
	# Read the keyrec file.
	#
	getkeyrecs($ARGV[0]);

	#
	# Give the output.
	#
	showzones()	if($zonesflag);
	showkeys();

	#
	# If the matching-record count should be given, give the count in
	# requested format.
	#
	if($cntflag)
	{
		if($terse)
		{
			print "$count\n";
		}
		else
		{
			my $plural = "s";
			$plural = "" if($count == 1);

			print "$count matching record$plural\n";
		}
	}
	return(0);
}

#-----------------------------------------------------------------------------
#
# Routine:	doopts()
#
sub doopts
{
	my $argc = shift;			# Command line argument count.

	GetOptions(\%options,@opts);
	$allflag	= $options{'all'}	|| 0;
	$zonesflag	= $options{'zones'}	|| 0;
	$keysflag	= $options{'keys'}	|| 0;
	$kskflag	= $options{'ksk'}	|| 0;
	$zskflag	= $options{'zsk'}	|| 0;
	$zcurflag	= $options{'cur'}	|| 0;
	$znewflag	= $options{'new'}	|| 0;
	$zpubflag	= $options{'pub'}	|| 0;

	$refflag	= $options{'ref'}	|| 0;
	$unrefflag	= $options{'unref'}	|| 0;
	$validflag	= $options{'valid'}	|| 0;
	$expiredflag	= $options{'expired'}	|| 0;

	$cntflag	= $options{'cnt'}	|| 0;
	$terse		= $options{'terse'}	|| 0;
	$long		= $options{'long'}	|| 0;

	#
	# Give a usage flag if asked.
	usage() if(defined($options{'help'}));

	#
	# Ensure we were given a keyrec file to check.
	#
	if($argc == 0)
	{
		usage();
		exit(1);
	}

	#
	# If the valid-zone or the expired-zone option was given, but the
	# zones specifier wasn't, we'll assume they want all the zones listed.
	#
	if(($validflag || $expiredflag)	 && !$zonesflag)
	{
		$zonesflag = 1;
	}

	#
	# If the referenced-keys option or the unreferenced-keys option was
	# given, but none of the key specifiers were, then we'll assume
	# they want all the keys listed.
	#
	if(($refflag || $unrefflag)				&&
	   (!$keysflag && !$kskflag  && !$zskflag &&
	    !$zcurflag && !$znewflag && !$zpubflag))
	{
		$keysflag = 1;
	}

	#############################################################
	#
	# WARNING:  Code order beyond this point is critical.  Do *NOT* modify
	#	    anything in the rest of this routine if you are an idiot.
	#

	#
	# Select all records if the "-all" option was given.  This option
	# overrides almost everything.
	#
	if($allflag)
	{
		$zonesflag  = 1;
		$keysflag   = 1;

		$refflag     = 0;
		$unrefflag   = 0;
		$validflag   = 0;
		$expiredflag = 0;

	}

	#
	# Set the appropriate keys flags.
	#
	if($keysflag)
	{
		$kskflag = 1;
		$zskflag = 1;
	}

	if($zskflag)
	{
		$zcurflag = 1;
		$znewflag = 1;
		$zpubflag = 1;
	}
}

#-----------------------------------------------------------------------------
#
# Routine:	getkeyrecs()
#
sub getkeyrecs
{
	my $krfile = shift;			# Keyrec file.

	keyrec_read($krfile);

	@krnames = keyrec_names();

	foreach my $krn (sort(@krnames))
	{
		my $kr;				# Reference to keyrec.
		my %keyrec;			# Keyrec.
		my $type;			# Keyrec's type.

		$kr = keyrec_fullrec($krn);
		%keyrec = %$kr;

		$type = $keyrec{'keyrec_type'};

		if($type eq 'zone')
		{
			$zones{$krn} = $kr;
		}
		elsif($type eq 'ksk')
		{
			$allkeys{$krn} = $kr;
			$kskkeys{$krn} = $kr;
		}
		elsif($type eq 'zskcur')
		{
			$allkeys{$krn} = $kr;
			$zcurkeys{$krn} = $kr;
			$zskkeys{$krn} = $kr;
		}
		elsif($type eq 'zsknew')
		{
			$allkeys{$krn} = $kr;
			$znewkeys{$krn} = $kr;
			$zskkeys{$krn} = $kr;
		}
		elsif($type eq 'zskpub')
		{
			$allkeys{$krn} = $kr;
			$zpubkeys{$krn} = $kr;
			$zskkeys{$krn} = $kr;
		}
	}
}

#-----------------------------------------------------------------------------
#
# Routine:	showzones()
#
sub showzones
{
	#
	# Loop through the zone list and give data on the desired zones.
	#
	foreach my $zk (sort(keys(%zones)))
	{
		my $krr = $zones{$zk};
		my %kr = %$krr;

		#
		# Bump the matching-records count.
		#
		$count++;

		#
		# Stay cloaked if only the count of matching records
		# should be given.
		#
		if($cntflag)
		{
			next;
		}

		#
		# Give the appopriate style of output.
		#
		if($terse)
		{
			print "$zk\n";
		}
		elsif($long)
		{
			print "zone $zk\t$kr{'zonefile'}\t$kr{'endtime'}\t\"$kr{'keyrec_signdate'}\"\n";
		}
		else
		{
			print "zone $zk\t$kr{'zonefile'}\t\t\"$kr{'keyrec_signdate'}\"\n";
		}
	}
}

#-----------------------------------------------------------------------------
#
# Routine:	showkeys()
#
sub showkeys
{
	my $krr;			# Reference to a key's keyrec.

	#
	# Print the information about the KSK keys.
	#
	if($kskflag)
	{
		foreach my $k (sort(keys(%kskkeys)))
		{
			$krr = $kskkeys{$k};
			outkey($k,$krr,"KSK-key","KSK");
		}
	}

	#
	# Print the information about the current ZSK keys.
	#
	if($zcurflag)
	{
		foreach my $k (sort(keys(%zcurkeys)))
		{
			$krr = $zcurkeys{$k};
			outkey($k,$krr,"ZSK-cur-key","ZSK-cur");
		}
	}

	#
	# Print the information about the new ZSK keys.
	#
	if($znewflag)
	{
		foreach my $k (sort(keys(%znewkeys)))
		{
			$krr = $znewkeys{$k};
			outkey($k,$krr,"ZSK-new-key","ZSK-new");
		}
	}

	#
	# Print the information about the published ZSK keys.
	#
	if($zpubflag)
	{
		foreach my $k (sort(keys(%zpubkeys)))
		{
			$krr = $zpubkeys{$k};
			outkey($k,$krr,"ZSK-pub-key","ZSK-pub");
		}
	}

}

#-----------------------------------------------------------------------------
#
# Routine:	refdkey()
#
# Purpose:	This routine determines if a named key is referenced by any
#		zones.  This doesn't distinguish between KSKs and any of the
#		ZSKs.  If found, 1 is returned; if not, 0 is returned.
#
sub refdkey
{
	my $kn = shift;			# Key name to be checked.
	my %zkr;			# Zone keyrec.
	my $zkrref;			# Reference to zone keyrec.
	my $zn;				# Zone name.

	#
	# Check each zone to see if it's using this key.
	#
	foreach $zn (keys(%zones))
	{
		$zkrref = $zones{$zn};
		%zkr = %$zkrref;

		if(($zkr{'kskkey'} eq $kn) ||
		   ($zkr{'zskcur'} eq $kn) ||
		   ($zkr{'zsknew'} eq $kn) ||
		   ($zkr{'zskpub'} eq $kn))
		{
			#
			# Found it!  Return success.
			#
			return(1);
		}
	}

	#
	# Didn't find a reference to the key, so we'll return failure.
	#
	return(0);
}

#-----------------------------------------------------------------------------
#
# Routine:	outkey()
#
sub outkey
{
	my $k	 = shift;		# Key name.
	my $kkr	 = shift;		# Reference to key's keyrec.
	my $long = shift;		# Long-output label.
	my $reg	 = shift;		# Regular-output label.

	my %kr = %$kkr;			# Key's keyrec.

	#
	# Check if this key is referenced or not.
	#
	if(($refflag   && !refdkey($k)) ||
	   ($unrefflag &&  refdkey($k)))
	{
		return;
	}

	#
	# Bump the matching-records count.
	#
	$count++;

	#
	# Run silent if only the count of matching records should be given.
	#
	if($cntflag)
	{
		return;
	}

	#
	# Give the output line appropriate for output flags.
	#
	if($terse)
	{
		print "$k\n";
	}
	elsif($long)
	{
		print "$long $k\t$kr{'zonename'}\t$kr{'algorithm'}\t$kr{'ksklength'}\t\"$kr{'keyrec_gendate'}\"\n";
	}
	else
	{
		print "$reg $k\t$kr{'zonename'}\t\t\"$kr{'keyrec_gendate'}\"\n";
	}
}

#-----------------------------------------------------------------------------
#
# Routine:	usage()
#
sub usage
{
	print STDERR "usage:  lskrf [options] <keyrec-file>\n";
	print STDERR "\trecord selection options:\n";
	print STDERR "\t\t-all		list all records\n";
	print STDERR "\t\t-zones\t	list all zones\n";
	print STDERR "\t\t-keys		list all keys\n";
	print STDERR "\t\t-ksk		list KSK keys\n";
	print STDERR "\t\t-zsk		list ZSK keys\n";
	print STDERR "\t\t-cur		list current ZSK keys\n";
	print STDERR "\t\t-pub		list published ZSK keys\n";
	print STDERR "\t\t-valid\t	show keyrecs of unexpired zones\n";
	print STDERR "\t\t-expired	show keyrecs of expired zones\n";
	print STDERR "\t\t-ref		show referenced key keyrecs\n";
	print STDERR "\t\t-unref\t	show unreferenced key keyrecs\n";
	print STDERR "\toutput format options:\n";
	print STDERR "\t\t-cnt		only give count of matching keyrecs\n";
	print STDERR "\t\t-terse\t	terse output\n";
	print STDERR "\t\t-long		long output\n";
	print STDERR "\t\t-h		help message \n";
	exit(0);
}

1;

##############################################################################
#

=pod

=head1 NAME

lskrf - List the fields in a dnssec-tools keyrec file.

=head1 SYNOPSIS

  lskrf [-z] [-k] [-ksk] [-zsk] [-cur] [-pub] [-cnt] [-unref] [-ref] [-t] [-l] [-h] <keyrec-file>

=head1 DESCRIPTION

This script lists the contents of a keyrec file.

=back

=head1 OPTIONS

=over 4

=item -k

Only perform checks of key I<keyrec>s.  This option may not be combined with
the B<-z> option.

=item -z

Only perform checks of zone I<keyrec>s.  This option may not be combined with
the B<-k> option.

=item -c

Display a final count of errors.

=item -q

Do not display messages.  This option supersedes the setting of the I<-v>
option.

=item -v

Display many messages.  This option is subordinate to the I<-q> option.

=item -h

Display a usage message.

=back

=head1 AUTHOR

Wayne Morrison, tewok@users.sourceforge.net

=head1 SEE ALSO

Net::DNS::SEC::Tools::keyrec.pm(3)

=cut

