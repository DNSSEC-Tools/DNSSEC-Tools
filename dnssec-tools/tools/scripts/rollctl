#!/usr/bin/perl
#
# Copyright 2006 SPARTA, Inc.  All rights reserved.  See the COPYING
# file distributed with this software for details
#
#
# rollctl
#
#	This script controls the roll-over daemon.
#	See the pod for more details.
#

use strict;

use Getopt::Long;

use Net::DNS::SEC::Tools::rollmgr;
use Net::DNS::SEC::Tools::tooloptions;

my $NOT_IMPLEMENTED = 0;				# Execution barrier.

#######################################################################

#
# Data required for command line options.
#
my %options = ();			# Filled option array.
my @opts =
(
	"halt",				# Shutdown rollerd.
	"logfile=s",			# Set rollerd's log file.
	"loglevel=s",			# Set rollerd's logging level.
	"rollall",			# Roll all our zones.
	"rollrec=s",			# Change the rollrec file.
	"rollzone=s",			# Roll the specified zone.
	"runqueue",			# Run the queue.
	"shutdown",			# Shutdown rollerd.
	"skipzone=s",			# Stop this specified zone from rolling.
	"sleeptime=i",			# Set rollerd's sleep time.
	"status",			# Get rollerd's status.
	"zonestatus",			# Get status of zones.

	"version",			# Display the version number.
	"help",				# Give a usage message and exit.
);

#
# Flags for the options.  Variable/option mapping should obvious.
#
my $logfileflag;
my $loglevelflag;
my $rollallflag;
my $rollrecflag;
my $rollzoneflag;
my $runqueueflag;
my $shutdownflag;
my $skipzoneflag;
my $sleeptimeflag;
my $statusflag;
my $zonestatflag;

my $version	= 0;			# Display the version number.

#######################################################################


my $ret;				# Return code from main().

$ret = main();
exit($ret);

#-----------------------------------------------------------------------------
#
# Routine:	main()
#
# Purpose:	Yeah, yeah, a main() isn't necessary.  However, it offends my
#		sense of aesthetics to have great gobs of code on the same
#		level as a pile of globals.
#
#		But what about all those globals, you ask...
#
sub main()
{
	my $argc = @ARGV;		# Number of command line arguments.

	my $ret;			# Return code.
	my $resp;			# Response message.

	#
	# Check our options.  All the commands are alphabetized, except
	# for shutdown.  We'll save that for last.
	#
	doopts($argc);

	#
	# Send commands for all the specified options.
	#
	if($logfileflag)
	{
		rollmgr_sendcmd(CHANNEL_WAIT,ROLLCMD_LOGFILE,$logfileflag);

		($ret, $resp) = rollmgr_getresp();
		if($ret == ROLLCMD_RC_OKAY)
		{
			print "rollerd log file set to $logfileflag\n";
		}
		else
		{
			print STDERR "rollctl:  log-level set failed:  $resp\n";
		}
	}
	if($loglevelflag)
	{
		rollmgr_sendcmd(CHANNEL_WAIT,ROLLCMD_LOGLEVEL,$loglevelflag);

		($ret, $resp) = rollmgr_getresp();
		if($ret == ROLLCMD_RC_OKAY)
		{
			print "rollerd log level set to $loglevelflag\n";
		}
		else
		{
			print STDERR "rollctl:  log-level set failed:  $resp\n";
		}
	}
	if($rollrecflag)
	{
		rollmgr_sendcmd(CHANNEL_WAIT,ROLLCMD_ROLLREC,$rollrecflag);

		($ret, $resp) = rollmgr_getresp();
		if($ret == ROLLCMD_RC_OKAY)
		{
			print "rollerd now using rollrec file $rollrecflag\n";
		}
		else
		{
			print STDERR "rollctl:  couldn't set rollrec file:  $resp\n";
		}
	}
	if($rollzoneflag)
	{
		rollmgr_sendcmd(CHANNEL_WAIT,ROLLCMD_ROLLZONE,$rollzoneflag);

		($ret, $resp) = rollmgr_getresp();
		if($ret == ROLLCMD_RC_OKAY)
		{
			print "$resp\n";
		}
		else
		{
			print STDERR "unable to force roll-over process for $rollzoneflag:  $resp\n";
		}
	}
	if($runqueueflag)
	{
		rollmgr_sendcmd(CHANNEL_WAIT,ROLLCMD_RUNQUEUE);

		($ret, $resp) = rollmgr_getresp();
		if($ret == ROLLCMD_RC_OKAY)
		{
			print "rollerd checking rollrec queue\n";
		}
		else
		{
			#
			# Shouldn't ever get here...
			#
			print STDERR "rollctl:  couldn't force the rollrec queue:  $resp\n";
		}
	}
	if($skipzoneflag)
	{
		rollmgr_sendcmd(CHANNEL_WAIT,ROLLCMD_SKIPZONE,$skipzoneflag);

		($ret, $resp) = rollmgr_getresp();
		if($ret == ROLLCMD_RC_OKAY)
		{
			print "roll-over stopped for zone $skipzoneflag\n";
		}
		else
		{
			print STDERR "unable to stop roll-over for zone $skipzoneflag:  \"$resp\"\n";
		}
	}
	if($sleeptimeflag)
	{
		rollmgr_sendcmd(CHANNEL_WAIT,ROLLCMD_SLEEPTIME,$sleeptimeflag);

		($ret, $resp) = rollmgr_getresp();
		if($ret == ROLLCMD_RC_OKAY)
		{
			print "rollerd sleep time set to $sleeptimeflag\n";
		}
		else
		{
			print STDERR "rollctl:  sleep-time set failed:  \"$resp\"\n";
		}
	}
	if($statusflag)
	{
		rollmgr_sendcmd(CHANNEL_WAIT,ROLLCMD_STATUS);

		($ret, $resp) = rollmgr_getresp();
		if($ret == ROLLCMD_RC_OKAY)
		{
			print "$resp";
		}
		else
		{
			print STDERR "rollctl:  status failed:  \"$resp\"\n";
		}
	}
	if($shutdownflag)
	{
		rollmgr_sendcmd(CHANNEL_WAIT,ROLLCMD_SHUTDOWN);
		($ret, $resp) = rollmgr_getresp();
		if($ret == ROLLCMD_RC_OKAY)
		{
			print "$resp\n";
		}
		else
		{
			print STDERR "rollctl:  shutdown failed:  \"$resp\"\n";
		}
	}
	if($zonestatflag)
	{
		rollmgr_sendcmd(CHANNEL_WAIT,ROLLCMD_ZONESTATUS);

		($ret, $resp) = rollmgr_getresp();
		if($ret == ROLLCMD_RC_OKAY)
		{
			print "$resp";
		}
		else
		{
			print STDERR "rollctl:  zonestatus failed:  \"$resp\"\n";
		}
	}

	if($NOT_IMPLEMENTED)
	{
		if($rollallflag)
		{
			rollmgr_sendcmd(CHANNEL_CLOSE,ROLLCMD_ROLLALL);
		}
	}

	return(0);
}

#-----------------------------------------------------------------------------
#
# Routine:	doopts()
#
# Purpose:	This routine shakes and bakes our command line options.
#		A bunch of option variables are set according to the specified
#		options.  Then a little massaging is done to make sure that
#		the proper actions are taken.  A few options imply others, so
#		the implied options are set if the implying options are given.
#
sub doopts
{
	my $argc = shift;			# Command line argument count.

	#
	# Give a usage flag if there aren't any options.
	#
	usage() if($argc == 0);

	#
	# Parse the options.
	#
	GetOptions(\%options,@opts) || usage();

	#
	# Set our option variables based on the parsed options.
	#
	$logfileflag	= $options{'logfile'}	 || 0;
	$loglevelflag	= $options{'loglevel'}	 || 0;
	$rollallflag	= $options{'rollall'}	 || 0;
	$rollrecflag	= $options{'rollrec'}	 || 0;
	$rollzoneflag	= $options{'rollzone'}	 || 0;
	$runqueueflag	= $options{'runqueue'}	 || 0;
	$shutdownflag	= ($options{'shutdown'}  || $options{'halt'})   || 0;
	$skipzoneflag	= $options{'skipzone'}	 || 0;
	$sleeptimeflag	= $options{'sleeptime'}	 || 0;
	$statusflag	= $options{'status'}	 || 0;
	$zonestatflag	= $options{'zonestatus'} || 0;
	$version        = $options{'version'}    || 0;

	#
	# Show the version number if requested
	#
	show_version() if(defined($options{'version'}));

	#
	# Give a usage flag if asked.
	#
	usage() if(defined($options{'help'}));

}

#----------------------------------------------------------------------
#
# Routine:	show_version()
#
# Purpose:	Print the version number(s) and exit.
#
sub show_version
{
	print STDERR "Version: 0.1\n";
	print STDERR "DNSSEC-Tools Version: 0.9.1\n";
	exit(1);
}


#-----------------------------------------------------------------------------
#
# Routine:	usage()
#
sub usage
{
	print STDERR "usage:  rollctl [options] \n";
	print STDERR "\t-halt				shutdown rollerd\n";
	print STDERR "\t-logfile <logfile>		set log file\n";
	print STDERR "\t-loglevel <loglevel>		set logging level\n";
	print STDERR "\t-rollall			roll all zones\n";
	print STDERR "\t-rollrec <rollrec>		set rollrec file\n";
	print STDERR "\t-rollzone <zone>		roll named zone\n";
	print STDERR "\t-runqueue			run queue\n";
	print STDERR "\t-shutdown			shutdown rollerd\n";
	print STDERR "\t-skipzone <zone>		skip named zone\n";
	print STDERR "\t-sleeptime <sleeptime>		set sleep time\n";
	print STDERR "\t-status				get rollerd's status\n";
	print STDERR "\t-zonestatus			get status of zones\n";
	print STDERR "\t-version			display version number\n";
	print STDERR "\t-help				help message \n";
	exit(0);
}

1;

##############################################################################
#

=pod

=head1 NAME

rollctl - Send commands to the DNSSEC-Tools roll-over daemon.

=head1 SYNOPSIS

  rollctl [options]

=head1 DESCRIPTION

The I<rollctl> command sends commands to the DNSSEC-Tools roll-over daemon,
I<rollerd>.  Multiple options may be specified on a single command line and
they will be executed in I<alphabetical> order.  The exception to this
ordering is that the I<-shutdown> command will always be executed last.

In most cases, I<rollerd> will send a response to I<rollctl>.  I<rollctl> will
print a success or failure message, as appropriate.

=head1 OPTIONS

The following options are handled by I<rollctl>.

=over 4

=item I<-halt>

Cleanly halts I<rollerd> execution.

=item I<-logfile E<lt>logfileE<gt>>

Sets the I<rollerd> log file to I<logfile>.
This must be a valid logging file, meaning that if I<logfile> already
exists, it must be a regular file.  The only exceptions to this are if
I<logfile> is B</dev/stdout> or B</dev/tty>.

=item I<-loglevel E<lt>loglevelE<gt>>

Sets the I<rollerd> logging level to I<loglevel>.
This must be one of the valid logging levels defined in B<rollmgr.pm(3)>.

=item I<-rollall>

Initiates roll-over for all the zones defined in I<rollerd>'s I<rollrec> file.

I<This option is not currently implemented.>

=item I<-rollrec E<lt>rollrec_fileE<gt>>

Sets I<rollrec> file to be processed by I<rollerd> to I<rollrec_file>.

=item I<-rollzone E<lt>zoneE<gt>>

Initiates roll-over for the zone named by I<zone>.

=item I<-runqueue>

Wakes up I<rollerd> and has it run its queue of I<rollrec> entries.

=item I<-shutdown>

Synonym for I<-halt>.

=item I<-skipzone E<lt>zoneE<gt>>

Stops roll-over for the zone named by I<zone>.

=item I<-sleeptime E<lt>sleeptimeE<gt>>

Sets I<rollerd>'s sleep time to I<sleeptime>.  I<sleeptime> must be an integer
at least as large as the B<$MIN_SLEEP> value in I<rollerd>.

=item I<-status>

Has I<rollerd> write several of its operational parameters to its log file.
The parameters are also reported to I<rollctl>, which prints them to the
screen.

=item I<-zonestatus>

Has I<rollerd> give the status of zones in the current I<rollrec> file to the
I<rollerd> log file.  The status is also reported to I<rollctl>, which prints
it to the screen.

For each zone in the I<rollrec> file, the zone name, the record type ("skip"
or "roll", and the current roll-over phase are given.

=item I<-help>

Displays a usage message.

=back

=head1 FUTURE

The following modifications may be made in the future:

=over 4

=item command execution order

The commands will be executed in the order given on the command line rather
than in alphabetical order.

=back

=head1 COPYRIGHT

Copyright 2006 SPARTA, Inc.  All rights reserved.
See the COPYING file included with the DNSSEC-Tools package for details.

=head1 AUTHOR

Wayne Morrison, tewok@users.sourceforge.net

=head1 SEE ALSO

B<Net::DNS::SEC::Tools::rollmgr.pm(3)>,
B<Net::DNS::SEC::Tools::rollrec.pm(3)>

B<rollerd(8)>

=cut
